-{
  This boolean trait provides:
  - foldable Bool
  - operation
}

data True
data False
type Bool = True | False

-- section foldable

trait foldable s for s {
    sig fold : forall b. self -> (True -> b) -> (False -> b) -> b
}

impl foldable True {
    def fold t _ = t self
}

impl foldable False {
    def fold _ f = f self
}

impl foldable Bool
    with foldable True
    with foldable False

-- The operation trait defines basic capabilities

trait operation b for b with foldable Bool {
    sig (||) : self -> self -> self
    sig (&&) : self -> self -> self
    sig not  : self -> self
    sig (=>) : self -> self -> self
    sig eval : self -> Bool
}

type bool = unit -> Bool

sig true : unit -> True
def true = { True }

sig false : unit -> False
def false = { False }

impl operation bool with foldable Bool {
    def (||) b = { self () fold true b }
    def (&&) b = { self () fold b false }
    def not    = { self () fold false true }
    def (=>) b = self not || b
    def eval   = self ()
}
